plugins {
    id("java-library")
    id("com.gradleup.shadow") version "8.3.5"
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id("de.eldoria.plugin-yml.paper") version "0.7.1"
}

group = "cloud.grabsky"
version = "1.21.6-${System.getenv("GITHUB_RUN_NUMBER") ?: "DEV"}"

repositories {
    mavenLocal()
    mavenCentral()
    // Repository containing the Paper API.
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    // Repository containing all my libraries.
    maven { url = "https://vela.grabsky.workers.dev/releases" }
}

dependencies {
    // Lombok
    compileOnly("org.projectlombok:lombok:1.18.36")
    annotationProcessor("org.projectlombok:lombok:1.18.36")
    // Paper API
    compileOnly("io.papermc.paper:paper-api:1.21.6-R0.1-SNAPSHOT")
    // Dependencies (My Libraries)
    paperLibrary("cloud.grabsky:bedrock:1.21-67")
    paperLibrary("cloud.grabsky:configuration-paper:1.21.4-54")
    // Dependencies (Lamp)
    paperLibrary("io.github.revxrsal:lamp.common:4.0.0-rc.12")
    paperLibrary("io.github.revxrsal:lamp.bukkit:4.0.0-rc.12")
    paperLibrary("io.github.revxrsal:lamp.brigadier:4.0.0-rc.12")
}

tasks {
    runServer {
        minecraftVersion("1.21.6")
    }
    shadowJar {
        shadowJar.archiveFileName = "${rootProject.name}-${project.version}.jar"
    }
    compileJava {
        options.fork = true
        // Setting compatibility to Java 21 (above should work too).
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }
}

paper {
    main = "cloud.grabsky.heads.Heads"
    loader = "cloud.grabsky.heads.Heads\$PluginLoader"
    apiVersion = "1.21.5"
    generateLibrariesJson = true
}

/* FOR GITHUB ACTIONS */

tasks.register("release") {
    doLast { print("${version.toString().split('-')[0]} #${System.getenv("GITHUB_RUN_NUMBER")}") }
}

tasks.register("tag") {
    doLast { print(version) }
}
